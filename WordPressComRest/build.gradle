buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

android {

    compileSdkVersion 19
    buildToolsVersion "21.1.2"

    defaultConfig {
        versionName "1.0.3"
        minSdkVersion 14
        targetSdkVersion 20
    }
}

dependencies {
    compile 'com.mcxiaoke.volley:library:1.0.16'
}

version android.defaultConfig.versionName
group = "com.automattic"
archivesBaseName = "rest"

afterEvaluate {

    configurations {
        archives {
            extendsFrom configurations.default
        }
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        configuration = configurations.archives
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                      authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                      authentication(userName: ossrhUsername, password: ossrhPassword)
                    }
                }

                pom.project {
                    name 'WordPressComRest'
                    packaging 'aar'
                    description 'WordPress.com REST Client for Android apps..'
                    url 'https://github.com/Automattic/android-wordpress-com-rest'
                    scm {
                        connection 'scm:git:https://github.com/Automattic/android-wordpress-com-rest.git'
                        developerConnection 'scm:git:https://github.com/Automattic/android-wordpress-com-rest.git'
                        url 'https://github.com/Automattic/android-wordpress-com-rest'
                    }

                    licenses {
                        license {
                            name 'The GPLv2 License'
                            url 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.html'
                        }
                    }

                    developers {
                        developer {
                            id 'daniloercoli'
                            name 'Danilo Ercoli'
                            email 'danilo@automattic.com'
                        }

                        developer {
                            id 'beaucollins'
                            name 'Beau Collins'
                            email 'robert@automattic.com'
                        }

                        developer {
                            id 'roundhill'
                            name 'Dan Roundhill'
                            email 'dan@automattic.com'
                        }

                        developer {
                            id 'maxme'
                            name 'Maxime Biais'
                            email 'maxime@automattic.com'
                        }

                        developer {
                            id 'hypest'
                            name 'Stefanos Togoulidis'
                            email 'stefanos.togoulidis@automattic.com'
                        }
                    }
                }
            }
        }
    }
}
